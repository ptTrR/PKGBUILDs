# Maintainer: graysky <graysky@archlinux.us>
# contributor: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

pkgname=raspberrypi-userland
pkgver=20201129
pkgrel=1
_commit=9f3f9054a692e53b60fca54221a402414e030335
pkgdesc="Firmware tools, libraries, and headers for Raspberry Pi 64-bit version only"
arch=('aarch64')
url="https://github.com/raspberrypi/userland"
license=('custom')
makedepends=('cmake')
replaces=('raspberrypi-firmware-tools')
provides=('raspberrypi-firmware-tools' 'raspberrypi-firmware')
options=(!strip)
source=("https://github.com/raspberrypi/userland/archive/${_commit}.tar.gz"
        "https://raw.githubusercontent.com/raspberrypi/firmware/master/opt/vc/LICENCE"
        '00-raspberrypi-firmware.conf')
md5sums=('f7bfa5c5f60399b2a533f315442e4302'
         '86e53f5f5909ee66900418028de11780'
         '72e0d5818fc513ece1b964f25f7e7882')

build() {
  cd ${srcdir}/userland-${_commit}

  cmake -DCMAKE_BUILD_TYPE=Release -DARM64=ON
  make
}

package() {
  mkdir -p "${pkgdir}"/opt/vc

  cd ${srcdir}/userland-${_commit}
  make install DESTDIR="${pkgdir}"
  cp ../LICENCE "${pkgdir}"/opt/vc
  rm -rf "${pkgdir}"/opt/vc/src

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
  cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
}
